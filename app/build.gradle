import com.android.build.gradle.internal.dsl.SigningConfig

apply plugin: 'com.android.application'

android {
    compileSdkVersion 26
    defaultConfig {
        applicationId "com.dw.imximeng"
        minSdkVersion 14
        targetSdkVersion 26
        signingConfig getReleasePassword("../private.properties")
        versionCode 1
        versionName "1.0.0.4"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        buildConfigField "String", "API_ENV", "\" ${apiDefault}\""
    }
    buildTypes {
        debug {
            multiDexEnabled true
            signingConfig getReleasePassword("../private.properties")
        }

        release {
            multiDexEnabled true
            debuggable false
            jniDebuggable false
            signingConfig getReleasePassword("../private.properties")
            renderscriptDebuggable false
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    //gradle 3.1以下版本修改apk名称的方式
//    applicationVariants.all { variant ->
//        variant.outputs.each { output ->
//            def outputFile = output.outputFile
//            if (outputFile != null && outputFile.name.endsWith('.apk') && !outputFile.name.endsWith('debug.apk')) {
//                def fileName = outputFile.name.replaceAll(outputFile.name,
//                        "imximeng-V${defaultConfig.versionName}-${defaultConfig.versionCode}-${releaseTime()}-${variant.buildType.name}.apk")
//                output.outputFile = new File(outputFile.parent, fileName)
//            }
//        }
//    }

    //gradle 3.1版本修改apk名称的方式
    applicationVariants.all { variant ->
        if (variant.buildType.name == "release") {
            variant.outputs.all { output ->
                def outputFile = output.outputFile
                if (outputFile != null && outputFile.name.endsWith('.apk')) {
                    // apk_渠道名-版本号-版本名称-编译时间.apk
                    def fileName = "imximeng-${defaultConfig.versionCode}-v${defaultConfig.versionName}-${releaseTime()}.apk"

                    outputFileName  = fileName
                }
            }
        }
    }
}

//获取时间戳
static def releaseTime() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd')
    return formattedDate
}

def getReleasePassword(propFileName) {
    SigningConfig signingConfig = new SigningConfig("signInfo")
    File propFile = file(propFileName)
    if (propFile.exists()) {
        Properties properties = new Properties()
        properties.load(new FileInputStream(propFile))
        signingConfig.storeFile = file(properties['STORE_FILE'])
        signingConfig.storePassword = properties['STORE_PASSWORD']
        signingConfig.keyAlias = properties['KEY_ALIAS']
        signingConfig.keyPassword = properties['KEY_PASSWORD']
        println('signConfig is exits')
    } else {
        println('sign config is null')
        signingConfig = null
    }
    return signingConfig
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.android.support:support-vector-drawable:26.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation('com.android.support:support-annotations:26.1.0') {
        force = true
    }
    implementation 'com.zhy:okhttputils:2.6.1'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:3.2.0'
    implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'com.umeng.sdk:common:1.5.3'
    implementation 'com.umeng.sdk:analytics:7.5.3'
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'com.githang:status-bar-compat:0.7'
    implementation 'com.youth.banner:banner:1.4.8'
    implementation 'com.android.support:multidex:1.0.1'
//    implementation files('libs/umeng-shareboard-widget-6.9.2.jar')
}
